-- CREATE TABLE: https://learn.microsoft.com/en-us/sql/t-sql/statements/create-table-azure-sql-data-warehouse?view=fabric&preserve-view=true
IF OBJECT_ID('dbo.dimPaymentType', 'U') IS NOT NULL
    DROP TABLE dbo.dimPaymentType;
GO

CREATE TABLE dbo.dimPaymentType (
    PaymentTypeKey int NOT NULL,
    PaymentTypeName varchar(100) NOT NULL
);
GO

-- Load data using COPY INTO
-- COPY INTO: https://learn.microsoft.com/en-us/sql/t-sql/statements/copy-into-transact-sql?view=fabric
COPY INTO dbo.dimPaymentType
FROM 'https://onelake.dfs.fabric.microsoft.com/f826a61a-1236-4dcf-b6ce-8e8ed0a66e4d/154e6c58-2a7f-416d-bccc-864b2936eb8c/Files/raw/payment_type/payment_type.csv'
WITH (
    FILE_TYPE = 'CSV', 
    FIRSTROW = 2);
GO

SELECT * FROM dbo.dimPaymentType;

-- Cross-database query over lakehouse
SELECT TOP (10) * FROM DemoLakehouse.dbo.trip_curated;
GO

-- Cross-database analytical query
SELECT p.PaymentTypeName, SUM(t.passenger_count) AS Passengers, COUNT(*) AS Trips
FROM dbo.dimPaymentType AS p
LEFT JOIN DemoLakehouse.dbo.trip_curated AS t
ON p.PaymentTypeKey = t.payment_type
GROUP BY p.PaymentTypeName
ORDER BY Passengers DESC;
GO

-- Create fact table using CTAS
-- CTAS: https://learn.microsoft.com/en-us/sql/t-sql/statements/create-table-as-select-azure-sql-data-warehouse?view=fabric
IF OBJECT_ID('dbo.factTrip', 'U') IS NOT NULL
    DROP TABLE dbo.factTrip;
GO

CREATE TABLE dbo.factTrip
AS
SELECT 
    ROW_NUMBER() OVER (ORDER BY (SELECT 1)) AS TripID,
    VendorID AS VendorKey,
    payment_type AS PaymentTypeKey,
    CAST(PickupDate AS date) AS PickupDate,
    COALESCE(passenger_count, 1) AS Passengers,
    trip_distance AS TripDistance,
    total_amount AS TotalAmount,
    tip_amount AS TipAmount
FROM DemoLakehouse.dbo.trip_curated;
GO

GRANT SELECT ON dbo.factTrip TO [user1@analyticsdemoorg.onmicrosoft.com];
GRANT SELECT ON dbo.dimPaymentType TO [user1@analyticsdemoorg.onmicrosoft.com];
GO

SELECT p.PaymentTypeName, SUM(t.Passengers) AS Passengers, COUNT(*) AS Trips
FROM dbo.dimPaymentType AS p
LEFT JOIN dbo.factTrip AS t
ON p.PaymentTypeKey = t.PaymentTypeKey
GROUP BY p.PaymentTypeName
ORDER BY Passengers DESC;

-- T-SQL surface area: https://learn.microsoft.com/en-us/fabric/data-warehouse/tsql-surface-area
